use crate::ast::{Operator, Variable, Sentence};
use std::str::FromStr;

grammar;

LABEL = "LABEL";
FUNC = "FUNCTION";

GOTO = "GOTO";
IF = "IF";
RETURN = "RETURN";
DEC = "DEC";
ARG = "ARG";
CALL = "CALL";
PARAM = "PARAM";
READ = "READ";
WRITE = "WRITE";

COLON = ":";
ASSIGN = ":=";

ID =  r"[a-z]\w*";

Number: usize = {
    r"\d+" => usize::from_str(&<>).unwrap()
}

ArthOpt: Operator = {
    "+" => Operator::Plus,
    "-" => Operator::Sub,
    "*" => Operator::Mul,
    "/" => Operator::Div,
}

RelOpt: Operator = {
    "==" => Operator::Equal,
    ">" => Operator::Greater,
    "<" => Operator::Less,
    ">=" => Operator::GreaterEqual,
    "<=" => Operator::LessEqual,
}

pub (crate) Label: String = {
    ID => <>.to_string()
}

pub (crate) Var: Variable = {
    r"#\d+" => Variable::Number(i64::from_str(&<>[1..]).unwrap()),
    r"&[a-z]\w*" => Variable::Pointer(<>[1..].to_string()),
    r"\*[a-z]\w*" => Variable::Deref(<>[1..].to_string()),
    Label => Variable::Id(<>),
}

pub Sentence: Sentence = {
    // label
    LABEL <Label> COLON         => Sentence::Label(<>),
    FUNC <Label> COLON          => Sentence::Func(<>),
    // assign
    <l: Var> ASSIGN <r: Var>    => Sentence::Assign{<>},
    <target: Var> ASSIGN <l: Var> <opt: ArthOpt> <r: Var> 
                                => Sentence::Arth{<>},
    <var: Var> ASSIGN CALL <func: Label>                
                                => Sentence::Call{<>},
    // condition
    GOTO <Label>                => Sentence::Goto(<>),
    IF <l: Var> <opt: RelOpt> <r: Var> GOTO <target: Label> 
                                => Sentence::IfGoto{<>},
    // function
    RETURN <Var>                => Sentence::Return(<>),
    ARG <Var>                   => Sentence::Arg(<>),
    PARAM <Var>                 => Sentence::Param(<>),
    // array
    DEC <var: Var> <size: Number> 
                                => Sentence::Dec{<>},
    // io
    READ <Var>                  => Sentence::Read(<>),
    WRITE <Var>                 => Sentence::Write(<>),
}